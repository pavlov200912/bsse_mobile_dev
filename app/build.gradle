plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.example.myapplication"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"
        buildConfigField "Boolean", "USE_MOCK_BACKEND_API", 'false'
        // TODO: Прописать адрес нашего сервера.
        buildConfigField "String", "BACKEND_API_URL", '""'
    }

    buildFeatures {
        viewBinding true
    }

    flavorDimensions "deployment-environment"

    productFlavors {
        mock {
            dimension "deployment-environment"
            applicationIdSuffix ".mock"
            versionNameSuffix ".mock"
            buildConfigField "Boolean", "USE_MOCK_BACKEND_API", 'true'
        }
        dev {
            dimension "deployment-environment"
            applicationIdSuffix ".dev"
            versionNameSuffix ".dev"
            buildConfigField "String", "BACKEND_API_URL", '""'
        }
        stage {
            dimension "deployment-environment"
            applicationIdSuffix ".stage"
            versionNameSuffix ".stage"
            buildConfigField "String", "BACKEND_API_URL", '""'
        }
        prod {
            dimension "deployment-environment"
            versionNameSuffix ".prod"
            // Despite the default config, all the variables are specified explicitly here,
            // because this product flavor is the most sensitive to errors.
            buildConfigField "Boolean", "USE_MOCK_BACKEND_API", 'false'
            buildConfigField "String", "BACKEND_API_URL", '""'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "pavlov-ivan-${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
}
kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}


dependencies {
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // HTTP client.
    def retrofitVersion = "2.9.0"
    def okhttpVersion = '4.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp-tls:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    // JSON parser.
    def moshiVersion = '1.12.0'
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    // AndroidX.
    def lifecycleVersion = "2.4.0-alpha03"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    // Image loading.
    def glideVersion = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.activity:activity-ktx:1.4.0-alpha01"

    // View binding helper.
    implementation "com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.4.4"

    // Video player
    def exoPlayerVersion = "2.15.0"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoPlayerVersion"


    def adapterDelegatesVersion = '4.3.0'
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl:$adapterDelegatesVersion"
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:$adapterDelegatesVersion"

    // Logger.
    implementation "com.jakewharton.timber:timber:4.7.1"

    // Navigation.
    def navigationVersion = "2.4.0-alpha09"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // System insets handling.
    // https://chrisbanes.github.io/insetter
    // https://github.com/chrisbanes/insetter
    implementation "dev.chrisbanes.insetter:insetter:0.6.0"

    // https://github.com/haroldadmin/NetworkResponseAdapter.
    implementation "com.github.haroldadmin:NetworkResponseAdapter:4.2.2"


    // Dependency injection.
    def hiltVersion = "2.38.1"
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
    implementation "androidx.hilt:hilt-navigation-fragment:1.0.0"
}